# @author shkschneider
# prompts/prompt_shk_setup (vanilla zsh compatible)

autoload -Uz promptinit || return

set -o prompt_percent # certain escape sequences that start with '%' are expanded
set -o prompt_subst # the prompt string is first subjected to parameter expansion, command substitution and arithmetic expansion
export ZLE_RPROMPT_INDENT=1 # used to give the indentation between the right hand side of the right prompt in the line editor
#                             -- if not set, the value 1 is used
ZSH_THEME_RPROMPT_POSITION_PROMPT=${ZSH_THEME_RPROMPT_POSITION_PROMPT:-"true"}
ZSH_THEME_NEWLINE=${ZSH_THEME_NEWLINE:-"false"}

# user@machine /p/a/t/h                              exit_status jobs vcs:status
# $

ZSH_THEME_PROMPT_PREFIX=${ZSH_THEME_PROMPT_PREFIX:-""} #"%{⁅%G%}"
ZSH_THEME_PROMPT_SPACER=${ZSH_THEME_PROMPT_SPACER:-"%{ %G%}"}
ZSH_THEME_PROMPT_SUFFIX=${ZSH_THEME_PROMPT_SUFFIX:-""} #"%{⁆%G%}"
ZSH_THEME_PROMPT_CHAR=${ZSH_THEME_PROMPT_CHAR:-"%{»%G%} "}
ZSH_THEME_PROMPT_DOTS=${ZSH_THEME_PROMPT_DOTS:-"%{…%G%}"}

_title() {
    if [[ $TERM =~ xterm* ]] ; then
        print -Pn '\e]2;'
        print -Pn '%n@%m:%c'
        print -Pn '\a'
    fi
}

_prompt() {
    PROMPT=''
    [ "$ZSH_THEME_NEWLINE" = "true" ] && PROMPT=$'\n'

    local _user=''
    [ "$USER" != "$(logname)" ] && _user='%n'
    local _host=''
    [ -n "$SSH_TTY$SSH_CONNECTION$SSH_CLIENT" ] && _host="%m"
    if [ -n "$_user$_host" ] ; then
        PROMPT+='%B%F{green}'"$ZSH_THEME_PROMPT_PREFIX"
        PROMPT+="$_user"
        [ -n "$_host" ] && PROMPT+='@'"$_host" || PROMPT+="$_host"
        PROMPT+="$ZSH_THEME_PROMPT_SUFFIX%f%b$ZSH_THEME_PROMPT_SPACER"
    fi

    PROMPT+='%B%F{blue}'"$ZSH_THEME_PROMPT_PREFIX"
    if [ ${#PWD} -gt 80 ] ; then
        type fpwd &>/dev/null && {
            PROMPT+=$(fpwd -f -h)
        } || {
            # mcornella <https://github.com/robbyrussell/oh-my-zsh/issues/5068#issuecomment-218780098>
            PROMPT+=${${:-/${(j:/:)${(M)${(s:/:)${(D)PWD:h}}#(|.)[^.]}}/${PWD:t}}//\/~/\~}
        }
    else
        PROMPT+='%~'
    fi
    PROMPT+="$ZSH_THEME_PROMPT_SUFFIX%f%b"

    PROMPT+=$'\n'

    [[ $(id -u) = 0 ]] && local _poweruser=true || local _poweruser=false
    $_poweruser && local _color="red" || local _color="white"
    PROMPT+="%B%F{$color}"
    if [ -n "$ZSH_THEME_PROMPT_CHAR" ] ; then
        PROMPT+="$ZSH_THEME_PROMPT_CHAR"
        [[ "$ZSH_THEME_PROMPT_CHAR[-1]" != ' ' ]] && PROMPT+=' '
    else
        $_poweruser && PROMPT+='#' || PROMPT+='$'
    fi
    PROMPT+='%f%b'

    PROMPT+="%{$reset_color%}"
    export PROMPT
}

source "${ZSHK:=$ZSH}/plugins/git-prompt/git-prompt.plugin.zsh" &>/dev/null
type git_prompt_info &>/dev/null && ZSH_THEME_RPROMPT_GIT="true" || XSH_THEME_RPROMPT_GIT="false"

_rprompt() {
    RPROMPT=''
    [ "$ZSH_THEME_RPROMPT_POSITION_PROMPT" = "true" ] && RPROMPT+=%{$'\e[1A'%} # one line up
    RPROMPT+='%B'
    if [[ $SHLVL -gt 1 ]] ; then
        RPROMPT+='%F{black}'"$ZSH_THEME_PROMPT_SPACER+$((SHLVL - 1))"'%f'
    fi
    RPROMPT+="%F{red}%(1?.$ZSH_THEME_PROMPT_SPACER%?.)%f"
    RPROMPT+="%F{magenta}%(1j.$ZSH_THEME_PROMPT_SPACER%j.)%f"
    if [ "$ZSH_THEME_RPROMPT_GIT" = "true" ] ; then
        local _git_prompt_info="$(git_prompt_info)"
        if [ -n "$_git_prompt_info" ] ; then
            RPROMPT+="$ZSH_THEME_PROMPT_SPACER"
            RPROMPT+='%F{yellow}'"$ZSH_THEME_PROMPT_PREFIX"
            RPROMPT+="$_git_prompt_info"
            RPROMPT+="$ZSH_THEME_PROMPT_SUFFIX"'%f'
        fi
    fi
    [ "$ZSH_THEME_RPROMPT_POSITION_PROMPT" = "true" ] && RPROMPT+=%{$'\e[1B'%} # one line down
    RPROMPT+='%b'"%{$reset_color%}"
    export RPROMPT
}

precmd() {
    _title
    _prompt
    _rprompt
}

# EOF
